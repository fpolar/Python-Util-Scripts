//Version 1.1.0
//Added FFmpeg.exe location verification error and basic setting writing implemented

global string $src_img_dir = "";
global string $out_mov_dir = "";
global string $ffmpeg_dir = "";
global string $img_names;
global string $sub_dir_cb[];
global string $sub_dir_tf[];


global proc setImageName (string $src_text_field) 
{
	global string $src_img_dir;
	$src_img_dir = `textField -q -text source_field`;
}

global proc setFFmpeg () 
{
	global string $ffmpeg_dir;
	$ffmpeg_dir = `textField -q -text ffmpeg_field`;
}

global proc browseFFmpeg () 
{
	string $filenames[] = `fileDialog2 -rf true -fm 1`;
	textField -edit -text $filenames[0] ffmpeg_field;
	setFFmpeg();
}
global proc setSource () 
{
	global string $src_img_dir;
	$src_img_dir = `textField -q -text source_field`;
}

global proc browseSource () 
{
	string $filenames[] = `fileDialog2 -rf true -fm 3`;
	textField -edit -text $filenames[0] source_field;
	setSource();
}

global proc setOutput () 
{
	global string $out_mov_dir;
	$out_mov_dir = `textField -q -text output_field`;
}

global proc browseOutput () 
{
	string $filenames[] = `fileDialog2 -rf true -fm 3`;
	textField -edit -text $filenames[0] output_field;
	setOutput();
}

global proc FFMpegSewWindow () 
{
	if (`window -exists "FFMpegSewWindow"`) deleteUI "FFMpegSewWindow";
	window -menuBar true -title "FFMpegSewWindow" -widthHeight 300 750 FFMpegSewWindow;
	menu -label "Settings" -tearOff false;
	    menuItem -label "Load Settings" -c ReadSettings;
	    menuItem -label "Save Settings" -c WriteSettings;

	global string $sub_dir_cb[];
	global string $sub_dir_tf[];
	$sub_dir_cb = {};
	$sub_dir_tf = {};

	formLayout -numberOfDivisions 100 ffmpeg_form;

	//text -w 100 ("Project: " + (basenameEx (`workspace -q -rd`)));

	radioButtonGrp
		-numberOfRadioButtons 2
		-label "OS:        "
		-labelArray2 "PC" "MAC"
		-select 1
		-columnWidth 1 50
		-columnWidth 2 100
		-columnWidth 2 100
	myRadBtnGrp;

	rowLayout -numberOfColumns 3 
	    -adjustableColumn true
	    -columnAlign  1 "left"
	    -columnAlign  2 "right"
		-columnWidth 1 60
		-columnWidth 2 110
	    ffmpeg_row;
		text -label "ffmpeg loc";
		textField -changeCommand setFFmpeg ffmpeg_field;
		button -label ("Browse") -command browseFFmpeg ffmpeg_browse; 
		setParent..;

	rowLayout -numberOfColumns 3 
	    -adjustableColumn true
	    -columnAlign  1 "left"
	    -columnAlign  2 "right"
		-columnWidth 1 50
		-columnWidth 2 110
	    source_row;
		text -label "Source";
		textField -changeCommand setSource source_field;
		button -label ("Browse") -command browseSource src_browse; 
		setParent..;

	rowLayout -numberOfColumns 3 
		-adjustableColumn true
		-columnAlign  1 "left"
		-columnAlign  2 "right"
		-columnWidth 1 50
		-columnWidth 2 110
		output_row;
		text -label "Output";
		textField -changeCommand setOutput output_field;
		button -label ("Browse") -command browseOutput out_browse; 
		setParent..;

	rowLayout -numberOfColumns 2 
		-adjustableColumn true
		-columnAlign  1 "left"
		-columnAlign  2 "right"
		-columnWidth 1 150
		submit_row;
		checkBox -label "Subdirectories" -onCommand "set_subdirectories($src_img_dir)" -offCommand hide_subdirectories sub_checkbox;
		button -label ("Make Movie") -command "FFMpegSew" MMPegMovie; 
		setParent..;

    separator -w 280 sep;

		rowColumnLayout -numberOfColumns 2 
			-adjustableColumn true
			-columnWidth 2 200
			-columnAlign  1 "left"
			-columnAlign  2 "right"
			sub_layout;

	formLayout -edit
		-attachForm myRadBtnGrp "top" 15
		-attachForm myRadBtnGrp "left" 30
		-attachForm ffmpeg_row "top" 45
		-attachForm ffmpeg_row "left" 20
		-attachForm source_row "top" 80
		-attachForm source_row "left" 30
		-attachForm output_row "top" 117
		-attachForm output_row "left" 30
		-attachForm submit_row "top" 150
		-attachForm submit_row "left" 30
		-attachForm sep "top" 190
		-attachForm sep "left" 10
		-attachForm sub_layout "top" 220
		-attachForm sub_layout "left" -330
	ffmpeg_form;  

	showWindow FFMpegSewWindow; 
}  

global proc hide_subdirectories(){

	formLayout -edit
		-attachForm sub_layout "left" -330
	ffmpeg_form;  
	clear_subs();
}

global proc set_subdirectories (string $dir)
{
	global string $src_img_dir;
	if($src_img_dir == ""){
		checkBox -e -v 0 sub_checkbox;
		error "please select a source directory";
	}

	//getting subdirectories
	//TODO filetest seems to think some files are directories
	string $files[] = `getFileList -fld $dir`;
	string $out_files[] = {};
	for ($f in $files){
		string $f_path = $dir +"/" + $f;
		string $x = `filetest -d $f_path`;
		if($x){
			$out_files[size($out_files)] = $f;
		}
	}

	//creating and saving checkboxes, storing related subdirectories
	global string $sub_dir_cb[];
	global string $sub_dir_tf[];
	clear_subs();
	int $i = 0;
	for ($f in $out_files){
		string $new_cb_name = ("sub_ch_"+$i);
		string $new_tf_name = ("sub_tf_"+$i);
		string $disable_cmd = "textField -edit -enable false "+ $new_tf_name;
		string $enable_cmd = "textField -edit -enable true "+ $new_tf_name;
		$sub_dir_cb[$i] = $new_cb_name;
		$sub_dir_tf[$i] = $new_tf_name;
		checkBox -label " " -v true -offCommand $disable_cmd -onCommand $enable_cmd $new_cb_name;
		textField -text $f $new_tf_name;
		$i = $i + 1;
	}
	formLayout -edit
		-attachForm sub_layout "left" 30
	ffmpeg_form;  
} 

global proc clear_subs(){
	global string $sub_dir_cb[];
	global string $sub_dir_tf[];
	if(size($sub_dir_cb) > 0){
		for ($cb in $sub_dir_cb){
			deleteUI $cb;
		}
		for ($tf in $sub_dir_tf){
			deleteUI $tf;
		}
	}
	$sub_dir_cb = {};
	$sub_dir_tf = {};
}

global proc FFMpegSew() {
	string $FFMCombiner;
	global string $src_img_dir;
	global string $out_mov_dir;
	global string $ffmpeg_dir;
	if($src_img_dir == "" || $out_mov_dir == ""){
		error "please select a source and output directory";
	}
	if($ffmpeg_dir == ""){
		error "please specify the location of ffmpeg.exe";
	}
	
	global string $ffmpeg_dir;
	string $ffmpeg = $ffmpeg_dir;
	// string $ffmpeg = "C:/Users/theon/Documents/ffmpeg/bin/ffmpeg.exe";

    if (`radioButtonGrp -q -select myRadBtnGrp` == 1) {
    	$ffmpeg = (`workspace -q -rd` + "scripts/ffmpeg/bin/ffmpeg.exe");
    }
    if (`radioButtonGrp -q -select myRadBtnGrp` == 2) {
    	$ffmpeg = (`workspace -q -rd` + "scripts/ffmpeg");
    }
    
	$ffmpeg = "C:/Users/theon/Documents/ffmpeg/bin/ffmpeg.exe";

	string $ffmpeg_input_vars = " -r 12 -f image2 -s 1280x720 -i ";
	string $ffmpeg_output_vars = "-vcodec libx264 -pix_fmt yuv420p -crf 25";

	global string $img_names;
	$img_names = "FF%d.tif";
	if(`checkBox -q -v sub_checkbox`){
		print "subs enabled";
		global string $sub_dir_cb[];
		global string $sub_dir_tf[];
		string $ts_list = "";

		int $subidx = 0;
		for ($cb in $sub_dir_cb){
			if(`checkBox -q -v $cb`){
				string $curr_subdir = `textField -q -tx $sub_dir_tf[$subidx]`;

				$ffmpeg_cmd = "shell " + $ffmpeg + $ffmpeg_input_vars + "\"" + $src_img_dir + "/" + $curr_subdir + "/" + $img_names + "\" "+ $ffmpeg_output_vars+" \"" + $src_img_dir + "/" + $curr_subdir + "/out.mp4\"";

				$cmd_out = system($ffmpeg_cmd);
				if($cmd_out != "") print $cmd_out;

				string $ts_path = $src_img_dir + "/" + $curr_subdir + "/temp.ts";

				$ffmpeg_temp_cmd = "shell "+$ffmpeg+" -i \""+$src_img_dir + "/" + $curr_subdir+"/out.mp4\" -c copy -bsf:v h264_mp4toannexb -f mpegts \"" + $ts_path + "\"";

				system($ffmpeg_temp_cmd);

				$ts_list = $ts_list + $ts_path +"|";

			}
			$subidx = $subidx + 1;
		}

		string $ffmpeg_combine_cmd = $ffmpeg + " -y -i \"concat:" + $ts_list + "\" -c copy -bsf:a aac_adtstoasc \"" + $out_mov_dir + "/Final.mp4\"";
		print $ffmpeg_combine_cmd;
		system($ffmpeg_combine_cmd);

		//may not have permissions to delete
		/*
		$subidx = 0;
		for ($cb in $sub_dir_cb){
			if(`checkBox -q -v $cb`){
				string $curr_subdir = `textField -q -tx $sub_dir_tf[$subidx]`;
				string $ts_path = "\""+$src_img_dir + "/" + $curr_subdir + "/temp.ts"+"\"";
				$ts_path = substituteAllString($ts_path, "/",  "\\");
				sysFile -delete ($ts_path);
				//system("shell del" + $ts_path);
			}
			$subidx = $subidx + 1;
		}
		*/
	}else{
		$ffmpeg_cmd = "shell " + $ffmpeg + $ffmpeg_input_vars + "\"" + $src_img_dir + "/" + $img_names + "\" "+ $ffmpeg_output_vars+" \"" + $out_mov_dir + "/out.mp4\"";
		$cmd_out = system($ffmpeg_cmd);
		if($cmd_out != "") print $cmd_out;
	}
 
 	//This deletes the temporary files created to sew them all together in mp4 format. 
	//for( $item in $scripts ){
	//	sysFile -delete ($s0x_movies + "temp" + $item + ".ts");
	//}
  }

// The below two global proc read in and write out settings to one file
global proc WriteSettings()
{
	global string $src_img_dir;
	global string $out_mov_dir;
	global string $ffmpeg_dir;
	// Create A String Array With Test Data
	string $myStrArray[] =
	    {
	    "textField -e -text \"" + $ffmpeg_dir + "\" ffmpeg_field;",
	    "textField -e -text \"" + $src_img_dir + "\" source_field;",
	    "textField -e -text \"" + $out_mov_dir + "\" output_field;"
	    };

    string $filePath = (`internalVar -usd` + "ffmpeg_stitch_Settings.txt") ;

$fileId = `fopen $filePath "w"` ;

// Print Array To File
for($line in $myStrArray)
fprint $fileId ($line+"\n") ;

// Close File
fclose $fileId ;
}

global proc ReadSettings()
{
	$filename = (`internalVar -usd` + "ffmpeg_stitch_Settings.txt");
	$fileId=`fopen $filename "r"`;
	string $nextLine = `fgetline $fileId`;
	while ( size( $nextLine ) > 0 ) {
	    print ( $nextLine );
	    eval ($nextLine);
	    $nextLine = `fgetline $fileId`;
	}
	fclose $fileId;

}

FFMpegSewWindow;